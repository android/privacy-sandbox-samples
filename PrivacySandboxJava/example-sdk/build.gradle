/*
 * Copyright (C) 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.api.attributes.ProductFlavorAttr
import javax.inject.Inject

plugins {
    id 'com.android.privacy-sandbox-sdk'
}

/*
 * This class serves to disambiguate which flavor of the underlying libraries to build against.
 * OEMs will need to update the "params" value of the dependencies.attributesSchema snippet below
 * to "oem".
 */
class FlavorPicker implements AttributeDisambiguationRule<ProductFlavorAttr> {
    private final String choice
    @Inject
    FlavorPicker(String choice) {
        this.choice = choice;
    }
    void execute(MultipleCandidatesDetails<ProductFlavorAttr> details) {
        for (value in details.candidateValues) {
            if (value.name == choice) {
                details.closestMatch(value)
            }
        }
    }
}

android {
    compileSdkPreview 'TiramisuPrivacySandbox'
    minSdkPreview 'TiramisuPrivacySandbox'
    namespace = "com.example.example-sdk"

    bundle {
        packageName = "com.example.privacysandbox.provider"
        sdkProviderClassName = "com.example.sdkimpl.SdkProviderImpl"
        setVersion(1, 0, 0)
    }
}

dependencies.attributesSchema
        .attribute(ProductFlavorAttr.of("androidSdk"))
        .disambiguationRules
        .add(FlavorPicker.class) {
            params = ["preview"] // OEMs will need to update this value to "oem"
        }

dependencies {
    include project(':sdk-implementation')
}
