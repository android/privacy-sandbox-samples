/*
 * Copyright (C) 2023 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

// A standard android library that uses a Runtime-enabled Ad library to show "ads".
android {
    namespace 'com.existing.sdk'
    privacySandbox {
        enable = true
    }
    defaultConfig {
        compileSdk 34
        compileSdkExtension 12
        targetSdk 34
        minSdk 21
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    // Note that here we depend on the example-sdk-bundle, not the example-sdk.
    //
    // The example-sdk library contains all the logic, but apps are not compiled using its full
    // classpath. Instead, the bundle contains information about the SDK API and the Android Gradle
    // Plugin uses it to generate sources and compile the app.
    debugImplementation project(':example-sdk-bundle')

    // Static dependency on inapp-mediatee-sdk-adapter.
    implementation project(':inapp-mediatee-sdk-adapter')

    // Required for backwards compatibility on devices where SDK Runtime is unavailable.
    implementation "androidx.privacysandbox.sdkruntime:sdkruntime-client:$privacy_sandbox_sdk_runtime_version"

    // This is required to display banner ads using the SandboxedUiAdapter interface.
    implementation "androidx.privacysandbox.ui:ui-core:$privacy_sandbox_ui_version"
    implementation "androidx.privacysandbox.ui:ui-client:$privacy_sandbox_ui_version"

    // This is required to use SDK ActivityLaunchers.
    implementation "androidx.privacysandbox.activity:activity-core:$privacy_sandbox_activity_version"
    implementation "androidx.privacysandbox.activity:activity-client:$privacy_sandbox_activity_version"

    implementation 'androidx.appcompat:appcompat:1.6.1'
}
